//file:noinspection GrUnresolvedAccess
plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}
dependencies {
    minecraft("com.mojang:minecraft:${minecraft_version}")
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    include(implementation("com.moulberry:mixinconstraints:1.1.0"))
    modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_version}")
    modImplementation("me.fzzyhmstrs:fzzy_config:${fzzy_config_version}")
    modApi("io.github.gameking1happy.gk1hcore:gk1hcore-fabric-1.21.1:${gk1hcore_version}")
    modApi("io.github.gameking1happy.gk1htags:gk1htags-fabric-1.21.1:${gk1htags_version}")
    modImplementation("com.terraformersmc:modmenu:${modmenu_version}")
    modImplementation("maven.modrinth:netherite-extras:${netheriteextras_version}")
    modRuntimeOnly("io.wispforest:owo-lib:${owo_version}")
    modImplementation("atonkish.reinfshulker:reinforced-shulker-boxes:${rsb_version}") {exclude group: "atonkish.reinfchest" exclude group: "com.misterpemodder"}
    modCompileOnly("atonkish.reinfcore:reinforced-core:4.0.2+1.21.1")
    modRuntimeOnly("net.fabricmc:fabric-language-kotlin:1.13.7+kotlin.2.2.21")
    modImplementation("maven.modrinth:accessorify:${accessorify_version}+1.21.1-fabric")
    modImplementation("io.wispforest:accessories-fabric:${accessories_version}+1.21.1")
}
repositories {
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}