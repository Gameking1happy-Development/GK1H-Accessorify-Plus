//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyImplicitNullArgumentCall
//file:noinspection GrUnresolvedAccess
plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'org.sinytra.adapter.userdev' version '1.2.1-SNAPSHOT'
}
repositories {
    maven {
        url = "https://thedarkcolour.github.io/KotlinForForge/"
    }
    maven {
        name = "Sinytra"
        url = "https://maven.sinytra.org/"
    }
}
neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            gameDirectory = project.file('runs/client')
            client()
        }
        data {
            data()
            gameDirectory = project.file('runs/data')
            // DataGen can be run by - "./gradlew :neoforge:runData" in Terminal.
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', rootProject.project('common').file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()

        }
        server {
            gameDirectory = project.file('runs/server')
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}
dependencies {
    implementation(jarJar("com.moulberry:mixinconstraints:1.1.0"))
    api("io.github.gameking1happy.gk1hcore:gk1hcore-neoforge-1.21.1:${gk1hcore_version}")
    accessTransformers("io.github.gameking1happy.gk1hcore:gk1hcore-neoforge-1.21.1:${gk1hcore_version}")
    api("io.github.gameking1happy.gk1htags:gk1htags-neoforge-1.21.1:${gk1htags_version}")
    implementation("me.fzzyhmstrs:fzzy_config:${fzzy_config_version}+neoforge")
    runtimeOnly("maven.modrinth:netherite-extras:${netheriteextras_version}")
    runtimeOnly("atonkish.reinfshulker:reinforced-shulker-boxes:${rsb_version}"){ exclude group: "atonkish.reinfchest" exclude group: "com.misterpemodder" exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" exclude group: "com.terraformersmc", module: "modmenu"}
    runtimeOnly("io.wispforest:owo-lib-neoforge:${owo_version}")
    compileOnly(rootProject.project.files(".gradle/loom-cache/remapped_mods/remapped/maven/modrinth/netherite-extras-8f637a66").getAsFileTree())
    compileOnly(rootProject.project.files(".gradle/loom-cache/remapped_mods/remapped/atonkish/reinfshulker/reinforced-shulker-boxes-8f637a66/3.2.1+1.21.1/reinforced-shulker-boxes-8f637a66-3.2.1+1.21.1.jar"))
    compileOnly(rootProject.project.files(".gradle/loom-cache/remapped_mods/remapped/atonkish/reinfcore/reinforced-core-8f637a66/4.0.2+1.21.1/reinforced-core-8f637a66-4.0.2+1.21.1.jar"))
    compileOnly(rootProject.project.files(".gradle/loom-cache/remapped_mods/remapped/me/shedaniel/cloth").getAsFileTree())
    runtimeOnly("org.sinytra.forgified-fabric-api:forgified-fabric-api:${ffapi_version}")
    runtimeOnly("maven.modrinth:connector-extras:${connector_extras_version}")
    runtimeOnly("thedarkcolour:kotlinforforge-neoforge:5.10.0")
    implementation("maven.modrinth:accessorify:${accessorify_version}+1.21.1-neoforge")
    implementation("io.wispforest:accessories-neoforge:${accessories_version}+1.21.1")
    additionalRuntimeClasspath("org.sinytra:connector:${connector_version}")
}