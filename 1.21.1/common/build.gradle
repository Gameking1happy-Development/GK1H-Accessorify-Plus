//file:noinspection GrUnresolvedAccess
plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}
repositories {
    maven {
        name = "GK1H Mod Resources"
        url = "https://raw.githubusercontent.com/Gameking1happy-Development/Mod-Resources/main/maven/"
    }
    maven {
        url = "https://thedarkcolour.github.io/KotlinForForge/"
    }
}
dependencies {
    compileOnly("org.spongepowered:mixin:0.8.5")
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly("io.github.llamalad7:mixinextras-common:0.5.0")
    annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0")
    implementation(jarJar("com.moulberry:mixinconstraints:1.1.0"))
    implementation("me.fzzyhmstrs:fzzy_config:${fzzy_config_version}+neoforge")
    api("io.github.gameking1happy.gk1hcore:gk1hcore-common-1.21.1:${gk1hcore_version}")
    compileOnly("thedarkcolour:kotlinforforge-neoforge:5.10.0")
    compileOnly(rootProject.project.files(".gradle/loom-cache/remapped_mods/remapped/maven/modrinth/netherite-extras-8f637a66").getAsFileTree())
    compileOnly(rootProject.project.files(".gradle/loom-cache/remapped_mods/remapped/atonkish/reinfshulker/reinforced-shulker-boxes-8f637a66/3.2.1+1.21.1/reinforced-shulker-boxes-8f637a66-3.2.1+1.21.1.jar"))
    compileOnly(rootProject.project.files(".gradle/loom-cache/remapped_mods/remapped/atonkish/reinfcore/reinforced-core-8f637a66/4.0.2+1.21.1/reinforced-core-8f637a66-4.0.2+1.21.1.jar"))
    compileOnly(rootProject.project.files(".gradle/loom-cache/remapped_mods/remapped/me/shedaniel/cloth").getAsFileTree())
    implementation("maven.modrinth:accessorify:${accessorify_version}+1.21.1-neoforge")
    compileOnly("io.wispforest:accessories-common:${accessories_version}+1.21.1-mojmap")
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
    generatedResources {
        canBeResolved = false
        canBeConsumed = true
    }
}
artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
    generatedResources file('src/generated/resources/')
}
sourceSets.main.resources.srcDir('src/generated/resources/') exclude(".cache/**")